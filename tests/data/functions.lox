// Testing a basic function
print "Testing a basic fibonacci function";
fun fib(limit) {
  var a = 0;
  var temp;

  for (var b = 1; a < limit; b = temp + b) {
    print a;
    temp = a;
    a = b;
  }
}

fib(10000);


// Testing return values for functions
print "Testing return values from a function";
fun add(a, b) {
  return a + b;
}

assert(add(1, 2) == 3, "add function should work correctly");

// Testing recursive functions and return stopping execution
print "Testing recursive fibonacci";
fun rfib(n) {
  if (n <= 1) return n;
  return rfib(n - 2) + rfib(n - 1);
}

for (var i = 0; i < 20; i = i + 1) {
  print rfib(i);
}

// Testing functions which capture a closure
print "Testing a closure capture";
fun makeCounter() {
  var i = 0;
  fun count() {
    i = i + 1;
    return i;
  }

  return count;
}

var counter = makeCounter();
assert(counter() == 1, "The first counter value should be 1");
assert(counter() == 2, "The second counter value should be 2");


// Anonymous functions
print "Testing anonymous functions";
fun thrice(fn) {
  for (var i = 1; i <= 3; i = i + 1) {
    fn(i);
  }
}

var i = 0;
thrice(fun (a) {
  i = i + 1;
  assert(a == i, "The function should receive the correct arguments");
});

assert(i == 3, "The function should have been called three times");

// Sensible scope closures based on order of variable definition
var scope = "outer";
{
  fun test() {
    return scope;
  }

  assert(test() == "outer", "The initial reference should be to the outer scope.");
  var scope = "inner";
  assert(test() == "outer", "The reference should remain consistent after we define a new variable.");
}